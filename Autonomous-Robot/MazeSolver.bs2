' {$STAMP BS2}
' {$PBASIC 2.5}

' Author: Kiet Lam
' Email: ktklam9@gmail.com
' Purpose: Boe-Bot maze solver


' Global states
objectDetected VAR Byte

' Parameteric variables (use these when calling the subroutines)
degreeCounter VAR Word ' Every degreeCounter ~0.2556 degrees
stepCounter   VAR Word ' Every stepCounter ~ 1.64383cm

' Private variables (don't touch these)
counter VAR Word
irDetectLeft VAR Byte
IRFrequency VAR Word


' Initialize the variables to good defaults
degreeCounter = 38 ' For 90 degrees turns
stepCounter   = 16 ' For ~10 cm
objectDetected = 0 ' 0 for no, 1 for yes
IRFrequency = 31000 ' Close range detection

GOSUB MAIN
END

MAIN:
  stepCounter = 3
  DO
    GOSUB detectObject
    IF (objectDetected = 1) THEN
      GOSUB turnCWise
      PAUSE 500
      GOSUB detectObject
      PAUSE 500
      IF (objectDetected = 1) THEN
        GOSUB turnCCWise
        PAUSE 500
        GOSUB turnCCWise
      ENDIF
    ELSE
      GOSUB goForward
    ENDIF
  LOOP
  RETURN


detectObject:
  ' Object detection here using Boe-Bot's IR Sensor (Actually works!)
  FREQOUT 8, 1, IRFrequency
  irDetectLeft = IN9

  IF (irDetectLeft = 1) THEN
    objectDetected = 0
  ELSE
    objectDetected = 1
  ENDIF

  RETURN


' Parametric variables used: stepCounter
' How to use: . set $stepCounter
'             . every 1 stepCounter ~ 1.64383cm
goForward:
  ' Move forward
  FOR counter = 1 TO stepCounter
    PULSOUT 13, 500
    PULSOUT 12, 1000
    PAUSE 50
  NEXT
  RETURN

' Parametric varibles used: degreeCounter
' How to use: . set $degreeCounter
'             . every 1 degreeCounter ~ 0.2556 degrees
turnCCWise:
  FOR counter = 1 TO degreeCounter
    PULSOUT 13, 700
    PULSOUT 12, 700
    PAUSE 20
  NEXT
  PAUSE 200
  RETURN

' Parametric varibles used: degreeCounter
' How to use: . set $degreeCounter
'             . every 1 degreeCounter ~ 0.2556 degrees
turnCWise:
  FOR counter = 1 TO degreeCounter
    PULSOUT 13, 800
    PULSOUT 12, 800
    PAUSE 20
  NEXT
  PAUSE 200
  RETURN